buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
        junitVersion = '4.12'
        selenideVersion = '3.9'
        dockerMachineHost = 'docker-machine ip dev'.execute().text.trim()
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot' 

tasks.withType(JavaCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    testCompile 'junit:junit'
    testCompile "com.codeborne:selenide:${selenideVersion}"
}

test {
    systemProperty 'remote', "http://${dockerMachineHost}:4444/wd/hub"
    systemProperty 'selenide.baseUrl', 'http://app:8080'
}
